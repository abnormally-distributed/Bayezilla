model {
  # likelihood: t-distribution 
  for ( i in 1:N ) {
    y[i] ~ dt( mu[i] , tau[gp[i]], nu)
    # inverse link function
    mu[i] <- Intercept[gp[i]] + sum( beta[1:P] * X[i,1:P] )
  }
  # adaptive prior
  # (i) varying intercept
  for ( j in 1:nGroups ) {
    Intercept[j] ~ dnorm(eta , tauIntc)
    # precision for t-likelihood
    tau[j] ~ dscaled.gamma(10, 3)
    sigma[j] <- sqrt( 1 / tau[j] * nu / (nu-2) ) # stdev
  }
  # fixed priors
  eta ~ dnorm( 0 , .0001)
  tauIntc ~ dscaled.gamma(1 , 3)
  # (ii) slopes
  for ( j in 1:P ) {
    beta[j] ~ dnorm( 0 , 0.7071068) 
  }
  
  # (iii) degrees of freedom for t-likelihood
  nu ~ dgamma(2, .01) T(3, )
}
